;------------------------
;  FILL SCREEN (ALPHA MODE ONLY)
;     THIS ROUTINE WILL INVERT EVERY PIXEL OF THE 
;     PAGE PASSED
;
;   FOR USE BY BASIC PROGRAMS ONLY.  ONE CALLING ARGUMENT.
;          CALL FILL(PAGE)	NOTE:VARIABLE MUST BE AN INTEGER
;-------------------------
CDSEG	SEGMENT
	ASSUME	CS:CDSEG
RVSCRA	PROC	FAR
	PUSH	BP
	MOV	BP,SP
;
	PUSH	DS
	PUSH	ES
	PUSH	SI
	PUSH	DI
;  GET CURRENT NO. OF COLUMNS ON SCREEN
	MOV	AX,0		;  SEGMENT ADDR FOR DATA
	MOV	ES,AX
	MOV	BX,44AH		;  OFFSET FOR VARIABLE '
	MOV	DX,ES:[BX]	;  GET CURRENT VALUE
	MOV	AX,2048		;  OFFSET INCR VALUE FOR 40 CLMNS
	MOV	CX,1000		;  NO. OF WORDS TO STORE
;
	CMP	DL,40		;  DOES CURRENT COLUMN NO. =40?
	JZ	F1		;  YES, SO CONTINUE 
	MOV	AX,4096		;  OFFSET INCR VALUE FOR 80 CLMNS
	MOV	CX,2000		;  NO. OF WORDS TO STORE
F1:
	MOV	SI,[BP] + 6	;  GET ADDR OF PARAM
	MOV	BX,[SI]		;  MOVE PAGE PASSED TO BX
	MUL	BX		;  AX NOW CONTAINS OFFSET VALUE
	MOV	DI,AX
	MOV	SI,AX
	MOV	AX,0B800H
	MOV	DS,AX
	MOV	ES,AX
	CLD			;  SET DRCT FLAG TO INCR
F2:
	LODSW			;  GET WORD FROM MEMORY
	XOR	AH,77H		;  INVERT ATTRIBUTE BITS
	STOSW			;  STORE WORD IN SAME PLACE
	LOOP	F2		;  DO UNTIL CX=0
;
	POP	DI
	POP	SI
	POP	ES
	POP	DS
	POP	BP
	RET	2
RVSCRA	ENDP
CDSEG	ENDS
	END
