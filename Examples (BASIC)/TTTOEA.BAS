10 ' FTG Data Systems TTTOE (TIC-TAC-TOE)
20 ' Version 2.00 (C)Copyright FTG Data Systems 1982
30 ' Licensed Material - Program Property of FTG Date Systems
40 ' Dave E. Boulanger - programs author
50 '
60 ' SWITCH TO COLOR ADAPTER
70 DEF SEG=0 : POKE &H410, (PEEK(&H410) AND &HCF) OR &H10
80 SCREEN 1,0,0,0 : SCREEN 0 : WIDTH 40 : LOCATE ,,1,6,7
90 DEFINT A-Z: KEY OFF
100 SCREEN 1,0 : WIDTH 40 : COLOR 0,1
110 '
120 COMPILE=0 ' SET THIS FLAG TO 1 FOR COMPILES AND 0 WHEN RUNNING INTERPRETIVE
130 '
140 DIM BOARD(3,3),T$(8),WINX(3),WINY(3),HOLDX(3),HOLDY(3)
150 MENU$="A:MENU"
160 CLS
170 PRINT
180 PRINT"          T I C - T A C - T O E"
190 PRINT
200 PRINT"          Copyright(C) 1982 by"
210 PRINT"            FTG DATA SYSTEMS"
220 PRINT
230 PRINT
240 PRINT"       USE THE LIGHT PEN TO SELECT"
250 PRINT"       WHICH SYMBOL YOU WOULD LIKE"
260 PRINT"       TO PLAY.  X WILL ALWAYS GO"
270 PRINT"       FIRST.  "
280 PRINT
290 PRINT"       A NEW BOARD WILL APPEAR AT"
300 PRINT"       THE END OF EACH GAME."
310 PRINT
320 PRINT"       THIS WILL PLAY A TOUGH GAME, "
330 PRINT"       BUT IT CAN BE DEFEATED, SO"
340 PRINT"       GOOD LUCK!!!"
350 PRINT : PRINT : PRINT
360 PRINT" TOUCH THE LIGHT PEN"
370 PRINT" TO THIS SQAURE TO BEGIN PLAY";
380 LINE(250,173)-(265,183),3,BF
390 PEN ON: IF PEN(3)=0 THEN 390 ELSE X=PEN(4): Y=PEN(5): IF (Y<173) OR (Y>183) THEN 390
400 SOUND 1500,1
410 FOR I=1 TO 3: FOR J=1 TO 3
420   BOARD(I,J)=0
430 NEXT J: NEXT I
440 T$=TIME$: MID$(T$,3)=" "
450 MID$(T$,6)=" ":X=(VAL(T$)/40)
460 RANDOMIZE X
470 REM
480 REM ******************************
490 REM * DRAW THE BOARD/TARGET PADS *
500 REM ******************************
510 REM
520 CLS
530 LINE(100,10)-(110,190),1,BF
540 LINE(210,10)-(220,190),1,BF
550 LINE(30,68)-(280,75),1,BF
560 LINE(30,128)-(280,135),1,BF
570   LINE(55,30)-(75,40),1,BF
580   LINE(150,30)-(170,40),1,BF
590   LINE(240,30)-(260,40),1,BF
600   LINE(55,100)-(75,110),1,BF
610   LINE(150,100)-(170,110),1,BF
620   LINE(240,100)-(260,110),1,BF
630   LINE(55,160)-(75,170),1,BF
640   LINE(150,160)-(170,170),1,BF
650   LINE(240,160)-(260,170),1,BF
660 REM
670 REM *************************
680 REM *  BEGIN THE GAME HERE  *
690 REM *************************
700 REM
710 MOVES = 0
720 LOCATE 25,2: PRINT "WILL YOU BE X-   , OR O-   ?   EXIT-";
730 LINE(125,192)-(138,200),2,BF
740 LINE(205,192)-(218,200),3,BF
750 LINE(297,192)-(310,200),1,BF
760 PEN ON: IF PEN(3) = 0 THEN 760 ELSE X = PEN(4): Y= PEN(5)
770 IF Y<190 THEN 760 ELSE IF (X>96) AND (X<138) THEN SOUND 1000,2 : OPP=1 : GOTO 830 ELSE IF (X>176) AND (X<218) THEN SOUND 1500,2 : OPP=0 : GOTO 1380 ELSE IF (X>248) AND (X<310) THEN SOUND 500,1 : GOTO 2750 ELSE 760
780 REM
790 REM *****************************
800 REM *   OPPONENT MOVE SECTION   *
810 REM *****************************
820 REM
830 PEN ON: MOVES=MOVES+1: IF MOVES>9 THEN 1620
840 LOCATE 25,2:PRINT"               YOUR MOVE              ";
850 IF PEN(3) = 0 THEN 850
860  X = PEN(4): Y = PEN(5)
870 REM
880 REM *  CHECK FOR VALID ROW  *
890 REM
900   IF Y<30 THEN 850 ELSE IF Y>40 THEN 920 ELSE ROW=1: Y=35 : GOTO 980
910 REM
920   IF Y<100 THEN 850 ELSE IF Y>110 THEN 940 ELSE ROW=2: Y=102: GOTO 980
930 REM
940   IF Y<160 THEN 850 ELSE IF Y>170 THEN 850 ELSE ROW = 3: Y=165
950 REM
960 REM *  CHECK FOR VALID COLUMN  *
970 REM
980   IF X<48 THEN 850 ELSE IF X>72 THEN 1000 ELSE COLMN=1:X=64 : GOTO 1090
990 REM
1000   IF X<144 THEN 850 ELSE IF X>168 THEN 1020 ELSE COLMN = 2:X=160: GOTO 1090
1010 REM
1020   IF X<232 THEN 850 ELSE IF X>256 THEN 850 ELSE COLMN = 3: X=258
1030 REM
1040 REM *  A VALID INDEX INTO THE BOARD
1050 REM    ARRAY HAS BEEN FOUND.  NOW
1060 REM    CHECK FOR MOVE INTO A VALID
1070 REM    SQUARE [BOARD(COLMN,ROW)=0] *
1080 REM
1090 IF BOARD(COLMN,ROW) = 0 THEN 1100 ELSE SOUND 500,2: GOTO 850
1100 SOUND 1000,1: BOARD(COLMN,ROW) = 10
1110 LINE(X-25,Y-15)-(X+25,Y+15),0,BF
1120 IF OPP=0 THEN GOSUB 1290: GOTO 1380 ELSE GOSUB 1180: GOTO 1380
1130 REM
1140 REM ******************************
1150 REM * PLACE AN X AT LOCATION X,Y *
1160 REM ******************************
1170 REM
1180 LINE(X-30,Y+15)-(X+20,Y-15),2:LINE -(X+30,Y-15),2:LINE -(X-20,Y+15),2: LINE -(X-30,Y+15),2
1190 PAINT(X,Y),2
1200 LINE(X+30,Y+15)-(X-20,Y-15),2: LINE -(X-30,Y-15),2:LINE -(X+20,Y+15),2: LINE -(X+30,Y+15),2
1210 PAINT(X+25,Y+14),2
1220 PAINT(X-25,Y-14),2
1230 RETURN
1240 REM
1250 REM ******************************
1260 REM * PLACE AN O AT LOCATION X,Y *
1270 REM ******************************
1280 REM
1290 CIRCLE(X,Y),27,3,0,6.28
1300 CIRCLE(X,Y),20,3,0,6.28
1310 PAINT (X,Y+21),3
1320 RETURN
1330 REM
1340 REM ****************************
1350 REM *     COMPUTER MOVES       *
1360 REM ****************************
1370 REM
1380 PEN OFF: MOVES=MOVES+1: IF MOVES>9 THEN 1620
1390 LOCATE 25,2:PRINT"                                      ";
1400 IF MOVES<3 THEN GOSUB 1880 ELSE GOSUB 1990
1410 IF STATUS=3 THEN 1610
1420 IF STATUS=4 THEN 1620
1430 REM
1440 REM * CONVERT INDEX VARIABLES(X,Y)
1450 REM   INTO SCREEN POSITIONS *
1460 REM
1470 BOARD(X,Y)=1
1480 ON X GOTO 1490,1500,1510
1490   X=64 : GOTO 1520
1500   X=160: GOTO 1520
1510   X=258
1520 ON Y GOTO 1530,1540,1550
1530   Y=35 : GOTO 1560
1540  Y=102: GOTO 1560
1550  Y=165
1560 LINE(X-25,Y-15)-(X+25,Y+15),0,BF
1570 IF OPP=1 THEN GOSUB 1290: GOTO 1590 ELSE GOSUB 1180: GOTO 1590
1580 REM
1590 IF STATUS<=1 THEN 830
1600 LOCATE 25,6:PRINT"           I  WIN!!!        ";: GOTO 1630
1610 LOCATE 25,7:PRINT"          YOU WIN!!!        ";: GOTO 1640
1620 LOCATE 25,7:PRINT"           WE TIE           "; : GOTO 1800
1630 OPP = (OPP + 1) MOD 2
1640 FOR I=1 TO 3: ON WINX(I) GOTO 1650,1660,1670
1650   WINX(I)=64 : GOTO 1680
1660   WINX(I)=160 : GOTO 1680
1670   WINX(I)=258
1680 ON WINY(I) GOTO 1690, 1700, 1710
1690   WINY(I)=35 : GOTO 1720
1700   WINY(I)=102 : GOTO 1720
1710   WINY(I)=165
1720 NEXT I
1730 FOR I=1 TO 4 : FOR J=1 TO 3
1740   X=WINX(J) : Y=WINY(J)
1750   LINE(X-35,Y-25)-(X+35,Y+25),0,BF
1760   NEXT J : FOR J=1 TO 3
1770     X=WINX(J) : Y=WINY(J)
1780   IF OPP=0 THEN GOSUB 1290 : GOTO 1790 ELSE GOSUB 1180
1790   NEXT J : NEXT I
1800 IF COMPILE=0 THEN 1810 ELSE FOR I=1 TO 19000: NEXT I
1810 FOR I=1 TO 1000 : NEXT I : GOTO 410
1820 REM
1830 REM ******************************
1840 REM   FIRST MOVE LOGIC.  TAKE THE
1850 REM   CENTER SQUARE OR ANY CORNER
1860 REM ******************************
1870 REM
1880 MOVE=2: STATUS=0
1890 IF BOARD(2,2)<>0 THEN 1910 ELSE BOARD(2,2)=1:X=2 : Y=2 : RETURN
1900 REM
1910 X=(RND(X)*2)+1:IF X=2 THEN 1910
1920 Y=(RND(Y)*2)+1:IF Y=2 THEN 1920
1930 IF BOARD(X,Y)<>0 THEN 1910 ELSE RETURN
1940 REM
1950 REM *************************
1960 REM *   SECOND MOVE LOGIC   *
1970 REM *************************
1980 REM
1990 GOSUB 2150'    GET BOARD STATUS
2000 IF STATUS<>0 THEN RETURN
2010 X=(RND(X)*2)+1: Y=(RND(Y)*2)+1
2020 IF BOARD(X,Y)<>0 THEN 2010 ELSE RETURN
2030 REM
2040 REM * DETERMINE STATUS OF CURRENT
2050 REM    BOARD CONFIGURATION *
2060 REM
2070 REM ON RETURN, WHEN STATUS=
2080 REM   0  -  CHOSE ANY OPEN SQUARE
2090 REM   1  -  CHOSE SQUARE X/Y
2100 REM   2  -  COMPUTER WINS
2110 REM   3  -  OPPONENT WINS
2120 REM
2130 REM
2140 REM * FIRST VERTICALLY *
2150 X3=0: X4=0
2160 FOR X=1 TO 3: SUM=0: FOR Y=1 TO 3
2170   HOLDX(Y)=X: HOLDY(Y)=Y
2180   IF BOARD(X,Y)<>0 THEN 2200
2190     X2=X: Y2=Y
2200   SUM = SUM + BOARD(X,Y)
2210   NEXT Y:GOSUB 2640
2220   IF STATUS=3 THEN 2580
2230   IF STATUS<>2 THEN 2240 ELSE FOR I=1 TO 3: WINX(I)=HOLDX(I): WINY(I)=HOLDY(I): NEXT I
2240 NEXT X
2250 REM
2260 REM * NOW HORIZONTALY *
2270 FOR Y=1 TO 3: SUM=0: FOR X=1 TO 3
2280   HOLDX(X)=X : HOLDY(X)=Y
2290   IF BOARD(X,Y)<>0 THEN 2310
2300     X2=X: Y2=Y
2310   SUM = SUM + BOARD(X,Y)
2320   NEXT X: GOSUB 2640:IF STATUS=3 THEN 2580
2330   IF STATUS<>2 THEN 2340 ELSE FOR I=1 TO 3: WINX(I)=HOLDX(I): WINY(I)=HOLDY(I): NEXT I
2340 NEXT Y
2350 REM
2360 REM * NOW DIAGONALY(LEFT TO RIGHT)
2370 SUM=0
2380 FOR X=1 TO 3
2390   HOLDX(X)=X: HOLDY(X)=X
2400   IF BOARD(X,X)<>0 THEN 2420
2410     X2=X: Y2=X
2420   SUM = SUM + BOARD(X,X)
2430 NEXT X: GOSUB 2640:IF STATUS=3 THEN 2580
2440   IF STATUS<>2 THEN 2470 ELSE FOR I=1 TO 3: WINX(I)=HOLDX(I): WINY(I)=HOLDY(I): NEXT I
2450 REM
2460 REM * NOW RIGHT TO LEFT *
2470 SUM = 0
2480 FOR Y=1 TO 3: X= 4-Y
2490   HOLDX(Y)=X: HOLDY(Y)=Y
2500   IF BOARD(X,Y)<>0 THEN 2520
2510     X2=X: Y2=Y
2520   SUM = SUM + BOARD(X,Y)
2530 NEXT Y: GOSUB 2640:IF STATUS=3 THEN 2580
2540 IF STATUS<>2  THEN 2550 ELSE FOR I=1 TO 3: WINX(I)=HOLDX(I): WINY(I)=HOLDY(I): NEXT I
2550 IF X3=0 THEN 2560 ELSE X=X3:Y=Y3: STATUS=2: RETURN
2560 IF X4=0 THEN 2570 ELSE X=X4:Y=Y4: STATUS=1: RETURN
2570 STATUS=0: RETURN
2580 FOR I=1 TO 3: WINX(I)=HOLDX(I): WINY(I)=HOLDY(I): NEXT I: RETURN
2590 REM
2600 REM USE SUM TO DETERMINE STATUS
2610 REM OF BOARD
2620 REM
2630 REM FIRST CHECK FOR WIN
2640 STATUS=0
2650 IF SUM<>30 THEN 2690 ELSE STATUS=3 : GOTO 2740
2660 REM
2670 REM NOW CHECK FOR WIN ON NEXT MOVE
2680 REM
2690 IF (SUM<>2) OR (X3<>0) THEN 2730 ELSE STATUS=2 : X3=X2: Y3=Y2 : GOTO 2740
2700 REM
2710 REM CHECK FOR BLOCK WITH NEXT MOVE
2720 REM
2730 IF SUM<>20 THEN 2740 ELSE STATUS=1 : X4=X2: Y4=Y2
2740 RETURN
2750 RUN MENU$
