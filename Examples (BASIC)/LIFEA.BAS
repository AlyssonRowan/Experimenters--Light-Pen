10 ' FTG Data Systems LIFE80
20 ' Version 2.00 (C)Copyright FTG Data Systems 1982
30 ' Licensed Material - Program Property of FTG Date Systems
40 ' Dave E. Boulanger - programs author
50 '
60 ' SWITCH TO COLOR ADAPTER
70 DEF SEG=0 : POKE &H410, (PEEK(&H410) AND &HCF) OR &H10
80 SCREEN 1,0,0,0 : SCREEN 0 : WIDTH 40 : LOCATE ,,1,6,7
90 DEFINT A-Z:DIM A(2,80,24),H(100) : KEY OFF : SCREEN 0,0,0,0 : CLS : WIDTH 80
100 '
110 COMPILE=0' SET THIS FLAG TO 1 WHEN COMPILING AND 0 FOR INTERPRETIVE
120 '
130 MENU$="A:MENU"
140 '
150 ' CHECK AND SEE IF CALLED FROM MENU
160 DEF SEG=0 : READ A$ : FOR I=1 TO 10 : IF PEEK(&H4F0+I)<>ASC(MID$(A$,I+1,1))     THEN 190 ELSE NEXT I
170 WDTH=PEEK(&H4FE) : IF (WDTH<>40) AND (WDTH<>80) THEN 190
180 DRIVE$=CHR$(PEEK(&H4FD)) + ":" : GOTO 270
190 LOCATE 2,25 : PRINT"WHICH VERSION WOULD YOU LIKE?"
200 PRINT : PRINT SPC(18);"40 COLUMN(TYPE 40)  OR  80 COLUMN(TYPE 80)"
210 POKE 1052,PEEK(1050)
220 WDTH$=INPUT$(2) :WDTH=VAL(WDTH$) :IF (WDTH=40) OR (WDTH=80) THEN 230 ELSE 200
230 DRIVE$="A:"
240 '
250 DATA "FTG IS HERE"
260 '
270 CLS
280 IF WDTH=80 THEN 530
290 PRINT"                               L I F E 4 0"
300 PRINT
310 PRINT"                           Copyright(C) 1982 by"
320 PRINT"                            F.T.G. DATA SYSTEMS"
330 PRINT
340 PRINT"    THE GAME OF LIFE, INVENTED BY JOHN CONWAY AND PRESENTED IN"
350 PRINT"    THE 'MATHEMATICAL GAMES' SECTION OF SCIENTIFIC AMERICAN IN"
360 PRINT"    OCTOBER 1970, IS REALLY A CASE STUDY OF CELLULAR AUTOMATA."
370 PRINT
380 PRINT"    THE GAME IS PLAYED ON A 40 BY 23 GRID (920 TOTAL CELLS)."
390 PRINT"    A CELL IS CONSIDERED ALIVE WHEN IT IS DIPLAYED, AND DEAD"
400 PRINT"    WHEN IT IS BLANK.  THE POPULATION OF CELLS IS GOVERNED BY"
410 PRINT"    3 SIMPLE RULES:"
420 PRINT
430 PRINT"  BIRTH    -  A DEAD CELL WILL BE ALIVE IN THE NEXT GENERATION"
440 PRINT"              IF EXACTLY THREE OF ITS NEIGHBORING CELLS ARE"
450 PRINT"              ALIVE IN THE CURRENT GENERATION."
460 PRINT"  SURVIVAL -  AN ALIVE CELL REMAINS ALIVE IN THE NEXT GENERATION"
470 PRINT"              IF TWO OR THREE OF ITS NEIGHBORING CELLS ARE ALIVE"
480 PRINT"              IN THE CURRENT GENERATION."
490 PRINT"  DEATH    -  AN ALIVE CELL WILL DIE IF FEWER THAN TWO, OR MORE"
500 PRINT"              THAN THREE OF ITS NEIGHBORING CELLS ARE ALIVE IN"
510 PRINT"              THE CURRENT GENERATION."
520 GOTO 760
530 PRINT"                               L I F E 8 0"
540 PRINT
550 PRINT"                           Copyright(C) 1982 by"
560 PRINT"                            F.T.G. DATA SYSTEMS"
570 PRINT
580 PRINT"    THE GAME OF LIFE, INVENTED BY JOHN CONWAY AND PRESENTED IN"
590 PRINT"    THE 'MATHEMATICAL GAMES' SECTION OF SCIENTIFIC AMERICAN IN"
600 PRINT"    OCTOBER 1970, IS REALLY A CASE STUDY OF CELLULAR AUTOMATA."
610 PRINT
620 PRINT"    THE GAME IS PLAYED ON AN 80 BY 23 GRID (1840 TOTAL CELLS)."
630 PRINT"    A CELL IS CONSIDERED ALIVE WHEN IT IS DIPLAYED, AND DEAD"
640 PRINT"    WHEN IT IS BLANK.  THE POPULATION OF CELLS IS GOVERNED BY"
650 PRINT"    3 SIMPLE RULES:"
660 PRINT
670 PRINT"  BIRTH    -  A DEAD CELL WILL BE ALIVE IN THE NEXT GENERATION"
680 PRINT"              IF EXACTLY THREE OF ITS NEIGHBORING CELLS ARE"
690 PRINT"              ALIVE IN THE CURRENT GENERATION."
700 PRINT"  SURVIVAL -  AN ALIVE CELL REMAINS ALIVE IN THE NEXT GENERATION"
710 PRINT"              IF TWO OR THREE OF ITS NEIGHBORING CELLS ARE ALIVE"
720 PRINT"              IN THE CURRENT GENERATION."
730 PRINT"  DEATH    -  AN ALIVE CELL WILL DIE IF FEWER THAN TWO, OR MORE"
740 PRINT"              THAN THREE OF ITS NEIGHBORING CELLS ARE ALIVE IN"
750 PRINT"              THE CURRENT GENERATION."
760 LOCATE 25,20,0 : PRINT "TOUCH THE PEN HERE ";CHR$(219);CHR$(219);
770 PRINT" TO CONTINUE";
780 PEN ON : IF PEN(3)=0 THEN 780 ELSE IF PEN(8)<25 THEN 780 ELSE SOUND 1000,1
790 CLS
800 PRINT"    THE LIGHT PEN IS USED IN THIS GAME TO CONTROL OR"
810 PRINT"    CHANGE THE SCREEN.  BY TOUCHING THE PEN TO THE"
820 PRINT"    APPROPRIATE WORD YOU CAN SELECT THE FUNCTION OF YOUR"
830 PRINT"    CHOICE."
840 PRINT
850 PRINT"    TO ENTER THE INITIAL GENERATION TOUCH THE WORD 'ADD'"
860 PRINT"    AND BEGIN PLACING CELLS IN THE WHITED AREA.  IF YOU WISH"
870 PRINT"    TO REMOVE ANY CELLS ON THE SCREEN TOUCH THE WORD"
880 PRINT"    'DELETE' AND THEN SIMPLY TOUCH THOSE CELLS."
890 PRINT
900 PRINT"    TO BEGIN PLAY TOUCH THE PEN TO THE WORD 'START'."
910 PRINT"    TOUCHING THE 'EXIT' WORD WILL RETURN YOU TO THE"
920 PRINT"    OPERATING SYSTEM OR MENU PROGRAM."
930 PRINT
940 PRINT"    THE GAME CAN BE STOPPED AT ANY TIME FOR ADDITIONS"
950 PRINT"    OR DELETIONS, AND RESUMED BY TOUCHING 'START'."
960 PRINT
970 PRINT"    WHENEVER A NEW CELL APPEARS A RANDOM SOUND IS GENERATED."
980 PRINT"    TO TURN THIS FEATURE OFF SIMPLY TOUCH THE PORTION OF THE"
990 PRINT"    SCREEN LABELED 'TURN SOUND OFF'."
1000 LOCATE 25,20,0 : PRINT "TOUCH THE PEN HERE ";CHR$(219);CHR$(219);
1010 PRINT" TO CONTINUE";
1020 PEN ON : IF PEN(3)=0 THEN 1020 ELSE IF PEN(8)<25 THEN 1020 ELSE SOUND 1000,1
1030 '
1040 '    BEGIN PROGRAM HERE
1050 CLS : SCREEN 0,0,0,0 : MODE=1 : WIDTH WDTH : LOCATE 1,1,0
1060 '             WHITE OUT THE SCREEN
1070 DEF SEG=&HB800 : IF COMPILE=1 THEN 1090
1080 FILL=12288 : BLOAD DRIVE$+"FLSCRA",FILL : I=0 : CALL FILL(I) : GOTO 1100
1090 FILL=12288 : BLOAD DRIVE$+"FLSCRA",FILL : I=0 : CALL ABSOLUTE (I,FILL)
1100 LOCATE 25,1 : PRINT SPACE$(WDTH);
1110 COLOR 0,7
1120 IF WDTH=80 THEN 1280 ELSE FOR I=0 TO 1
1130   SCREEN 0,0,I,0
1140   COLOR 7,0 : LOCATE 24,1  : PRINT "_____";
1150   COLOR 0,7 : LOCATE 25,1  : PRINT "START";
1160   COLOR 7,0 : LOCATE 24,7  : PRINT "____";
1170   COLOR 0,7 : LOCATE 25,7 : PRINT "STOP";
1180   COLOR 7,0 : LOCATE 24,13 : PRINT "___";
1190   COLOR 0,7 : LOCATE 25,13 : PRINT "ADD";
1200   COLOR 7,0 : LOCATE 24,17 : PRINT "______";
1210   COLOR 0,7 : LOCATE 25,17 : PRINT "DELETE";
1220   COLOR 7,0 : LOCATE 24,25 : PRINT "____";
1230   COLOR 0,7 : LOCATE 25,25 : PRINT "EXIT";
1240   COLOR 7,0 : LOCATE 24,31 : PRINT "_________";
1250   COLOR 0,7 : LOCATE 25,31 : PRINT "SOUND OFF";
1260 NEXT I
1270 GOTO 1430
1280 FOR I=0 TO 1
1290   SCREEN 0,0,I,0
1300   COLOR 7,0 : LOCATE 24,7  : PRINT "_____";
1310   COLOR 0,7 : LOCATE 25,6  : PRINT CHR$(221);"START";CHR$(222);
1320   COLOR 7,0 : LOCATE 24,18 : PRINT "____";
1330   COLOR 0,7 : LOCATE 25,17 : PRINT CHR$(221);"STOP";CHR$(222);
1340   COLOR 7,0 : LOCATE 24,29 : PRINT "___";
1350   COLOR 0,7 : LOCATE 25,28 : PRINT CHR$(221);"ADD";CHR$(222);
1360   COLOR 7,0 : LOCATE 24,38 : PRINT "______";
1370   COLOR 0,7 : LOCATE 25,37 : PRINT CHR$(221);"DELETE";CHR$(222);
1380   COLOR 7,0 : LOCATE 24,51 : PRINT "____";
1390   COLOR 0,7 : LOCATE 25,50 : PRINT CHR$(221);"EXIT";CHR$(222);
1400   COLOR 7,0 : LOCATE 24,62 : PRINT "______________";
1410   COLOR 0,7 : LOCATE 25,61 : PRINT CHR$(221);"TURN SOUND OFF";CHR$(222);
1420 NEXT I
1430 COLOR 7,0
1440 SCREEN 0,0,0,0 : SND=1
1450 '
1460 '
1470 '
1480 SCREEN 0,0,0,0
1490 PEN ON : IF PEN(3)=0 THEN 1490 ELSE X=PEN(9) : Y=PEN(8) : PEN OFF
1500 IF Y<>25 THEN ON MODE GOTO 1490, 1780, 1840
1510 IF WDTH=80 THEN 1640
1520 IF (X<1) OR (X>5) THEN 1530 ELSE SOUND 1000,1 : MODE=1 : GOTO 1880
1530 IF (X<7) OR (X>10) THEN 1540 ELSE SOUND 1000,1 : MODE=1 : GOTO 1490
1540 IF (X<13) OR (X>15) THEN 1550 ELSE SOUND 1000,1 : MODE=2 : GOTO 1490
1550 IF (X<17) OR (X>22) THEN 1560 ELSE SOUND 1000,1 : MODE=3 : GOTO 1490
1560 IF (X<25) OR (X>28) THEN 1570 ELSE SOUND 1000,1 : GOTO 2200
1570 IF (X<31) OR (X>39) THEN 1490
1580 COLOR 0,7 : SOUND 1000,1 : LOCATE 25,31,0 : IF SND=1 THEN PRINT "SOUND ON "; : SND=0 : GOTO 1590 ELSE PRINT "SOUND OFF"; : SND=1
1590 SCREEN 0,0,1,0 : LOCATE 25,31,0 : IF SND=0 THEN PRINT "SOUND ON "; : GOTO 1600 ELSE PRINT "SOUND OFF";
1600 COLOR 7,0
1610 SCREEN 0,0,0,0:IF COMPILE=0 THEN 1620 ELSE FOR I=1 TO 9000:NEXT I:GOTO 1830
1620 FOR I=1 TO 500 : NEXT I : GOTO 1490
1630 '
1640 IF (X<7) OR (X>11) THEN 1650 ELSE SOUND 1000,1 : MODE=1 : GOTO 1880
1650 IF (X<18) OR (X>21) THEN 1660 ELSE SOUND 1000,1 : MODE=1 : GOTO 1490
1660 IF (X<29) OR (X>31) THEN 1670 ELSE SOUND 1000,1 : MODE=2 : GOTO 1490
1670 IF (X<38) OR (X>43) THEN 1680 ELSE SOUND 1000,1 : MODE=3 : GOTO 1490
1680 IF (X<51) OR (X>54) THEN 1690 ELSE SOUND 1000,1 : GOTO 2200
1690 IF (X<62) OR (X>75) THEN 1490
1700 COLOR 0,7 : SOUND 1000,1 : LOCATE 25,62,0 : IF SND=1 THEN PRINT "TURN SOUND ON "; : SND=0 : GOTO 1710 ELSE PRINT "TURN SOUND OFF"; : SND=1
1710 SCREEN 0,0,1,0 : LOCATE 25,62,0 : IF SND=0 THEN PRINT "TURN SOUND ON "; : GOTO 1720 ELSE PRINT "TURN SOUND OFF";
1720 COLOR 7,0
1730 SCREEN 0,0,0,0:IF COMPILE=0 THEN 1740 ELSE FOR I=1 TO 9000:NEXT I : GOTO 1490
1740 FOR I=1 TO 500 : NEXT I : GOTO 1490
1750 '
1760 '   ADD SECTION
1770 '
1780 IF Y>23 THEN 1490
1790 LOCATE Y,X,0 :PRINT CHR$(1); :SCREEN 0,0,1,0 :LOCATE Y,X : PRINT CHR$(1);
1800 SCREEN 0,0,0,0 : GOTO 1490
1810 '
1820 '  DELETE SECTION
1830 '
1840 IF Y>23 THEN 1490
1850 LOCATE Y,X,0 : PRINT CHR$(219); : SCREEN 0,0,1,0 :   LOCATE Y,X : PRINT " "; : SCREEN 0,0,0,0 : GOTO 1490
1860 '
1870 '
1880 SCREEN 0,0,1,1 : INDX1 = 1 : INDX2 = 2
1890 IF WDTH=80 THEN 1910
1900 COLOR 23 : LOCATE 25,12 : PRINT" PLEASE WAIT "; : COLOR 7 : GOTO 1930
1910 COLOR 23 : LOCATE 25,35 : PRINT"PLEASE WAIT"; : COLOR 7
1920 '
1930 FOR I=1 TO WDTH : FOR J=1 TO 23 : A(1,I,J)=0 : NEXT J : NEXT I
1940 FOR I=1 TO WDTH : FOR J=1 TO 23
1950  A(2,I,J) = 0
1960 A2 = SCREEN(J,I) : IF A2 = 32 THEN 1990
1970  A(2,I,J) = &HA00
1980  FOR I2=I-1 TO I+1 : FOR J2=J-1 TO J+1 : A(1,I2,J2) = A(1,I2,J2) + 1     : NEXT J2 : NEXT I2 : A(1,I,J) = A(1,I,J) + 10
1990 NEXT J : NEXT I
2000 '
2010 IF WDTH=80 THEN 2040
2020  LOCATE 25,12,0 : PRINT " "; : COLOR 0,7 : PRINT "ADD"; : COLOR 7,0
2030 PRINT " "; :COLOR 0,7 :PRINT "DELETE"; :COLOR 7,0 :PRINT " "; : GOTO 2070
2040  LOCATE 25,35 : PRINT "  "; : COLOR 0,7 : PRINT CHR$(221);"DELETE";CHR$(222); : COLOR 7,0
2050 PRINT " ";
2060 '
2070 FOR Y2=1 TO 23 : FOR X2=1 TO WDTH
2080 PEN ON : IF PEN(3) = 0 THEN 2090 ELSE Y=PEN(8) : IF (Y>23) OR (Y=1) THEN SOUND 1500,2 : GOTO 1480
2090    CELL1=A(INDX1,X2,Y2) AND &HFF : CELL2=A(INDX2,X2,Y2)
2100    A(INDX1,X2,Y2)=256 * CELL1
2110    IF CELL1<10 THEN 2140 ELSE IF CELL2>=&HA00 THEN 2160
2120    LOCATE Y2,X2 : PRINT CHR$(1); : IF SND=0 THEN 2130 ELSE SOUND RND*500+500,.5
2130    SCREEN 0,0,0,1 : LOCATE Y2,X2 : PRINT CHR$(1); : SCREEN 0,0,1,1 : GOTO 2160
2140    IF CELL2<&HA00 THEN 2160 ELSE LOCATE Y2,X2 :PRINT " "; :SCREEN 0,0,0,1
2150    LOCATE Y2,X2 : PRINT CHR$(219); : SCREEN 0,0,1,1
2160    IF CELL1=3 THEN 2170 ELSE IF CELL1=13 THEN 2170 ELSE IF CELL1=14 THEN 2170 ELSE 2180
2170    A(INDX2,X2,Y2)=A(INDX2,X2,Y2)+10: FOR X3=X2-1 TO X2+1: FOR Y3=Y2-1 TO Y2+1: A(INDX2,X3,Y3)=A(INDX2,X3,Y3)+1:NEXT Y3: NEXT X3
2180  NEXT X2 : NEXT Y2
2190 I2=INDX1: INDX1=INDX2: INDX2=I2: GOTO 2070
2200 RUN MENU$
